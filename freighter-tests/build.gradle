import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'kotlin'
apply plugin: 'idea'


repositories {
    maven { url "${publicArtifactURL}/freighter-dev" }
}

sourceSets {
    freighterTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/freighterTest/kotlin')
        }
    }
}

evaluationDependsOn(":workflows")
task freighterTest(type: Test, dependsOn: [project(":workflows").jar]) {
    maxParallelForks 2
    systemProperty "java.util.concurrent.ForkJoinPool.common.parallelism", "128"
    testClassesDirs = sourceSets.freighterTest.output.classesDirs
    classpath = sourceSets.freighterTest.runtimeClasspath
    useJUnitPlatform {
        includeTags "DOCKER"
        excludeTags "AZURE", "FULL_LINUX_KERNEL", "ORACLE"
    }
}

configurations {
    freighterTestImplementation.extendsFrom testImplementation
    freighterTestRuntime.extendsFrom testRuntime
}

dependencies {
    freighterTestImplementation "freighter:freighter-testing-core-junit5:0.7.3-TEST-SNAPSHOT"

    freighterTestImplementation project(":contracts")
    freighterTestImplementation project(":workflows")
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.2"
        apiVersion = "1.2"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}


