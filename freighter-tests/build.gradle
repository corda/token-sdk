import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'kotlin'
apply plugin: 'idea'


repositories {
    maven { url "http://ci-artifactory.corda.r3cev.com/artifactory/freighter-dev" }
}

sourceSets {
    freighterTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/freighterTest/kotlin')
        }
    }
}

evaluationDependsOn(":workflows")
task freighterTest(type: Test, dependsOn: [project(":workflows").jar]) {
    maxParallelForks 2
    systemProperty "java.util.concurrent.ForkJoinPool.common.parallelism", "128"
    testClassesDirs = sourceSets.freighterTest.output.classesDirs
    classpath = sourceSets.freighterTest.runtimeClasspath
    useJUnitPlatform {
        includeTags "DOCKER"
        excludeTags "AZURE", "FULL_LINUX_KERNEL", "ORACLE"
    }

    testLogging {
        showStandardStreams = true
    }

}

configurations {
    freighterTestCompile.extendsFrom testCompile
    freighterTestRuntime.extendsFrom testRuntime
}

dependencies {
    freighterTestCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    freighterTestCompile "freighter:freighter-testing-core-junit5:0.9.0-SNAPSHOT"

    freighterTestCompile project(":contracts")
    freighterTestCompile project(":workflows")
    freighterTestCompile project(":workflows-integration-test")
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.2"
        apiVersion = "1.2"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}


