apply plugin: 'org.jetbrains.kotlin.plugin.jpa'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.cordapp'

cordapp {
    targetPlatformVersion 14
    minimumPlatformVersion 14
    workflow {
        name "Token SDK Workflows Integration Test"
        vendor "R3"
        licence "Apache 2"
        versionId 2
    }
    signing {
        enabled false
    }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
    test {
        resources {
            srcDir rootProject.file("config/test")
        }
    }
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/kotlin')
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    // Corda dependencies.
    cordaProvided("$corda_release_group:corda-core:$corda_release_version") {
        changing = true
    }

    cordaProvided("$corda_release_group:corda-node-api:$corda_release_version") {
        changing = true
    }

    // Logging.
    testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"

    // Testing.
    testImplementation "$corda_release_group:corda-node:$corda_release_version"
    testImplementation "$corda_release_group:corda-node-driver:$corda_release_version"
    testImplementation "$corda_release_group:corda-test-common:$corda_release_version"
    testImplementation "$corda_release_group:corda-core-test-utils:$corda_release_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.assertj:assertj-core:${assertj_version}"
    testImplementation "junit:junit:$junit_version"
    testImplementation "io.reactivex:rxjava:$rxjava_version"

    // CorDapp dependencies.
    cordapp project(":workflows")
    cordapp project(":contracts")
    cordapp "$confidential_id_release_group:ci-workflows:$confidential_id_release_version"

    implementation project(":modules:contracts-for-testing")
    implementation project(":modules:selection")
    implementation "org.postgresql:postgresql:42.2.8"
    implementation "org.slf4j:slf4j-api:$slf4j_version"

    testImplementation project(":modules:contracts-for-testing")
}

quasar {
    excludePackages.addAll(
            "antlr**",
            "com.codahale**",
            "com.fasterxml.**",
            "com.github.benmanes.caffeine.**",
            "com.google.**",
            "com.lmax.**",
            "com.zaxxer.**",
            "net.bytebuddy**",
            "io.github.classgraph**",
            "io.netty*",
            "liquibase**",
            "net.i2p.crypto.**",
            "nonapi.io.github.classgraph.**",
            "org.apiguardian.**",
            "org.bouncycastle**",
            "org.codehaus.**",
            "org.h2**",
            "org.hibernate**",
            "org.jboss.**",
            "org.objenesis**",
            "org.w3c.**",
            "org.xml**",
            "org.yaml**",
            "rx**",
            "kotlin**"
    )
}

task integrationTest(type: Test, dependsOn: []) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    jvmArgs test_module_opens
    jvmArgs test_module_exports
}

jar {
    archiveBaseName = "workflows-integration-test"
}
